buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'

group = 'org.distributev.mailmerger'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile project(':server')
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-tomcat'
}

bootWar {
    baseName = 'mailmerger'
    manifest {
        attributes 'Start-Class': 'org.distributev.mailmerger.app.Application'
    }
}

sourceSets {
    main {
        java {
            srcDir "${project(':server').projectDir}/src/main/java"
        }
    }
}

processResources.dependsOn(':client:build')

processResources {
    from ("${project(':client').projectDir}/dist/mailmerger") {
        into '/public'
    }

    from ("${project(':server').projectDir}/src/main/resources") {
        into '/'
    }

    from ("rundeck-config.properties") {
        into '/'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}